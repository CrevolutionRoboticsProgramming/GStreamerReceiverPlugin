import org.gradle.internal.os.OperatingSystem

/**
 * """Borrowed""" from Team 2053, the Tigertronics (https://github.com/team2053tigertronics/RobotPoseShuffleboardPlugin)
 */

plugins {
    id 'java-library'
}

ext.FRC_YEAR = "2019"

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

def getFrcToolsRoot ()
{
    if (OperatingSystem.current ().isWindows () )
    {
        String pubFolder = System.getenv("PUBLIC")
        if (pubFolder == null)
        {
            pubFolder = "C:\\Users\\Public"
        }

        return pubFolder + "/frc${FRC_YEAR}"
    }
    else
    {
        return System.getProperty ("user.home") + "/frc${FRC_YEAR}"
    }
} 

def getDeployDest ()
{
    return System.getProperty ("user.home") + "/Shuffleboard/plugins"
}

def getShuffleboardJar ()
{
    return files(getFrcToolsRoot() + "/tools/shuffleboard.jar")
}

task deployWidget (type: Copy, group: "Shuffleboard Widget", description: "Copy the widget jar into the Shuffleboard plugin folder", dependsOn: "build") {
    from "build/libs"
    into getDeployDest ()
    include "*.jar"
}

dependencies {
    compile getShuffleboardJar ()
    testImplementation 'junit:junit:4.12'
}